#
# A simple, tweaked version of the sorin theme that displays git information using the Solarized Color Scheme
#
# Authors:
#   Bob Williams <bobwilliams.ii@gmail.com>
#
# Shout Outs & Inspiration:
#  Alex Reece - http://codearcana.com/posts/2013/06/11/my-zsh-theme.html
#  Steve Losh - http://stevelosh.com/blog/2010/02/my-extravagant-zsh-prompt/
#
# Screenshots:
#

#  Solarized Color Scheme
#  http://ethanschoonover.com/solarized
#
#   SOLARIZED HEX     16/8 TERMCOL  XTERM/HEX   L*A*B      RGB         HSB
# --------- ------- ---- -------  ----------- ---------- ----------- -----------
# base03    #002b36  8/4 brblack  234 #1c1c1c 15 -12 -12   0  43  54 193 100  21
# base02    #073642  0/4 black    235 #262626 20 -12 -12   7  54  66 192  90  26
# base01    #586e75 10/7 brgreen  240 #585858 45 -07 -07  88 110 117 194  25  46
# base00    #657b83 11/7 bryellow 241 #626262 50 -07 -07 101 123 131 195  23  51
# base0     #839496 12/6 brblue   244 #808080 60 -06 -03 131 148 150 186  13  59
# base1     #93a1a1 14/4 brcyan   245 #8a8a8a 65 -05 -02 147 161 161 180   9  63
# base2     #eee8d5  7/7 white    254 #e4e4e4 92 -00  10 238 232 213  44  11  93
# base3     #fdf6e3 15/7 brwhite  230 #ffffd7 97  00  10 253 246 227  44  10  99
# yellow    #b58900  3/3 yellow   136 #af8700 60  10  65 181 137   0  45 100  71
# orange    #cb4b16  9/3 brred    166 #d75f00 50  50  55 203  75  22  18  89  80
# red       #dc322f  1/1 red      160 #d70000 50  65  45 220  50  47   1  79  86
# magenta   #d33682  5/5 magenta  125 #af005f 50  65 -05 211  54 130 331  74  83
# violet    #6c71c4 13/5 brmagenta 61 #5f5faf 50  15 -45 108 113 196 237  45  77
# blue      #268bd2  4/4 blue      33 #0087ff 55 -10 -45  38 139 210 205  82  82
# cyan      #2aa198  6/6 cyan      37 #00afaf 60 -35 -05  42 161 152 175  74  63
# green     #859900  2/2 green     64 #5f8700 60 -20  65 133 153   0  68 100  60


# Load dependencies.
pmodload 'helper'
pmodload 'last_command'

function battery_charge {
    echo `python ~/workspace/misc-scripts/battery.py3`
}

function prompt_lambda_command_time {
  time_to_human $last_command_time
}

function prompt_lambda_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_lambda_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_lambda_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format "%B%F{160}...%f%b"
  zstyle ':prezto:module:editor:info:keymap:primary' format '%B%F{160}❯%F{136}❯%F{64}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{160}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{64}❮%F{136}❮%F{160}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%%B%F{136}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{64}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{136}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{136}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format '%F{166}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{64}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{160}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{33}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ':%F{160}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{125}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{33}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{136}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{254}◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '[%f$(coalesce "%b" "%p" "%c")%A%B%S%a%d%m%r%U%u%s]' \
    'rprompt' ''

  # Define prompts.
  PROMPT='
%F{64}%n@%m%f %F{166}λ%f %F{37}%~%f ${git_info:+${(e)git_info[prompt]}}
%(!. %B%F{160}#%f%b.)${editor_info[keymap]} '
  RPROMPT='${(e)git_info[rprompt]} [%F{230}%D{%L:%M:%S %p}%f] [$(battery_charge)] [%F{37}$(prompt_lambda_command_time)%f ]'
  SPROMPT='zsh: correct %F{160}%R%f to %F{64}%r%f [nyae]? '
}

prompt_lambda_setup "$@"